apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'asm-license.txt'
        exclude 'NOTICE'
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
    }
    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'io.reactivex:rxandroid:1.0.0'
    compile 'joda-time:joda-time:2.7'

    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
    androidTestCompile 'org.hamcrest:hamcrest-integration:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.0'
}

task androidJar(type: Jar) {
    from 'build/intermediates/classes/release'
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidJar
    archives androidSourcesJar
}

version = "0.1.3"

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            groupId 'com.rejasupotaro'
            artifactId 'octodroid'
            version version
            artifact androidJar
            artifact androidSourcesJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'octodroid')
                root.appendNode('description', 'Android toolkit for the GitHub API.')
                root.appendNode('url', 'https://github.com/rejasupotaro/octodroid')

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', 'https://github.com/rejasupotaro/octodroid/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'scm:https://github.com/rejasupotaro/octodroid')
                scm.appendNode('connection', 'scm:git@github.com:rejasupotaro/octodroid.git')
                scm.appendNode('developerConnection', 'scm:git@github.com:rejasupotaro/octodroid.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'MIT')
                license.appendNode('url', 'https://raw.githubusercontent.com/rejasupotaro/octodroid/master/LICENSE.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? bintrayUser : ""
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? bintrayApiKey : ""
}

bintray {
    user = bintrayUserProperty
    key = bintrayApiKeyProperty
    publications = ['mavenAndroid']
    dryRun = false
    pkg {
        repo = 'maven'
        name = 'octodroid'
        licenses = ['MIT']
    }
}
